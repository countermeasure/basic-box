# vim: filetype=fish

###############################################################################
#
#  Change to a parent directory.
#
#  Arguments:
#    Int (optional). The number of parent directories to climb by. Values can
#    be 1 to 99 inclusive.
#
###############################################################################
function go_to_parent_directory
    if test -z $argv[1]
        cd ..
    else if not contains $argv[1] (seq 1 99)
        echo 'Argument must be an integer between 1 and 99'
        return 1
    else
        set path ''
        for i in (seq $argv[1])
            set path ../$path
        end
        cd $path
    end
end

###############################################################################
#
#  Allow pip to run if inside a virtualenv. If not inside a virtualenv, show a
#  message to prefer pipx.
#
#  Arguments:
#    Strings. The arguments to pass to the pip executable.
#
###############################################################################
function only_allow_pip_in_virtualenvs
    if test -n "$VIRTUAL_ENV"
        command pip $argv
    else
        echo 'Prefer pipx to pip when not in a virtualenv'
        return 1
    end
end

###############################################################################
#
#  Allow pip3 to run if inside a virtualenv. If not inside a virtualenv, show a
#  message to prefer pipx.
#
#  Arguments:
#    Strings. The arguments to pass to the pip3 executable.
#
###############################################################################
function only_allow_pip3_in_virtualenvs
    if test -n "$VIRTUAL_ENV"
        command pip3 $argv
    else
        echo 'Prefer pipx to pip3 when not in a virtualenv'
        return 1
    end
end

###############################################################################
#
#  Show a message saying that one command is preferred to another, but run the
#  less preferred command anyway.
#
#  For use in aliases.
#
#  Arguments:
#    Strings. The more preferred command, then the less preferred command.
#
###############################################################################
function prefer
    command $argv[2..-1]
    # If the alias is not being passed to a pipe, show the prefer message.
    if test -t 1
        echo
        set_color yellow
        echo "ðŸ’¡ Prefer $argv[1] to $argv[2]"
        set_color normal
        return 1
    end
end

###############################################################################
#
#  Show a message saying that one command should be used instead of another,
#  and don't run the less preferred command.
#
#  For use in aliases.
#
#  Arguments:
#    Strings. The more preferred command, then the less preferred command.
#
###############################################################################
function protect
    echo
    set_color yellow
    echo "âœ‹ Use $argv[1] instead of $argv[2]"
    set_color normal
    return 1
end

###############################################################################
#
#  Search the web.
#
#  For use in aliases.
#
#  Arguments:
#    Strings. The phrase to search for.
#
###############################################################################
function search_the_web
    # TODO: Make an equivalent function/alias for Bash?
    set query (string join + $argv)
    firefox --new-tab "https://duckduckgo.com/?q=$query&t=h_&ia=web"
end

###############################################################################
#
#  Set the window title to the truncated current working directory.
#
###############################################################################
function set_window_title
    set condensed_pwd (pwd | sed "s|^$HOME|~|")
    set separators_in_condensed_pwd \
        (string match --all --regex '/' $condensed_pwd)
    set non_home_directory_count (count $separators_in_condensed_pwd)
    set non_home_directories_to_show 2
    if test $non_home_directory_count -gt $non_home_directories_to_show
        set base_title (
            echo $condensed_pwd \
                | rev \
                | cut -d / -f -$non_home_directories_to_show \
                | rev
        )
        set window_title â€¦/$base_title
    else
        set window_title $condensed_pwd
    end
    echo "ðŸ“‚  $window_title"
end

###############################################################################
#
#  Sync one directory with another.
#
#  The contents of the second directory will be modified so that it is an exact
#  copy of the first directory.
#
#  Arguments:
#    Strings. The path to the directory to sync from, then the path of the
#    directory to be synced.
#
###############################################################################
function sync_directory
    # TODO: Make an equivalent function/alias for Bash?
    echo "This will make $argv[2] into an exact copy of $argv[1]."
    echo
    echo "Everything in $argv[2] which is not in $argv[1] will be deleted."
    echo
    while true
        read --prompt-str 'Do you want to continue? [y/n] ' confirm
        switch $confirm
            case Y y
                echo
                rsync --archive --delete --info=progress2 $argv[1]/ $argv[2]
                return 0
            case N n
                echo
                echo 'Nothing done.'
                return 1
        end
    end
end

###############################################################################
#
#  Compare the contents of two directories.
#
#  Arguments:
#    Strings. The paths to the two directories.
#
###############################################################################
function compare_directories
    python /home/user/.config/fish/compare_directories.py $argv[1] $argv[2]
end

###############################################################################
#
#  Tail a log on a remote server with syntax highlighting.
#
#  Arguments:
#    Strings. The server name, then the path to the log file.
#
###############################################################################
function tail_remote
    if test -z $argv[1]
        echo
        echo "Call this command like so:"
        echo "tail_remote <server> <file_path>"
        return 1
    end

    ssh $argv[1] tail -f $argv[2] | bat --language=log --paging=never --plain
end
